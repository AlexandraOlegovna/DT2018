\import lect03

-- 1. Докажите ассоциативность ++.

\func \infixr 5 ++ {A : \Type} (xs ys : List A) : List A \elim xs
  | nil => ys
  | cons x xs => cons x (xs ++ ys)

\func ++-assoc {A : \Type} (xs ys zs : List A) : (xs ++ ys) ++ zs = xs ++ (ys ++ zs) \elim xs
  | nil => idp
  | cons x xs => pmap (cons x) (++-assoc xs ys zs)

-- 2. На прошлой лекции мы определяли функцию T : Bool -> \Type рекурсивно.
--    Определите аналогичную функцию индуктивно.


\data T (b : Bool) \with
  | true => A

\func absurd {A : \Type} (e : T false) : A

-- 3. Напишите функцию lookup, которая принимает список и натуральное число и возвращает элемент по заданому индексу.
--    В общем случае эту функцию определить невозможно, т.к. индекс может быть больше, чем число элементов в списке.
--    Поэтому эта функция должна дополнительно еще принимать доказательство того, что индекс находится в допустимых границах, в виде T (n < length xs).

\func \infix 4 < (x y : Nat) : Bool
  | 0, 0 => false
  | 0, suc y => true
  | suc x, 0 => false
  | suc x, suc y => x < y


\func length {A : \Type} (xs : List A) : Nat \with
  | nil => 0
  | cons x xs => suc (length xs)


\func lookup {A : \Type} (xs : List A) (n : Nat) (e : T (n < length xs)) : A \elim xs, n
  | nil, 0 => absurd e
  | nil, suc n => absurd e
  | cons x xs, 0 => x
  | cons x xs, suc n => lookup xs n e

-- 4. Реализуйте аналоги функции replicate для vec и Vec (эта функция создает список заданной длины, состоящий из повторений данного элемента).

\func replicate1 {A : \Type} (x : A) (n : Nat) : vec A n \elim n
  | 0 => ()
  | suc n => (x, replicate1 x n)


\func replicate2 {A : \Type} (x : A) (n : Nat) : Vec A n \elim n
  | 0 => fnil
  | suc n => fcons x (replicate2 x n)

-- 5. Реализуйте аналоги функции map для vec и Vec.

\func map1 {A B : \Type} {n : Nat} (f : A -> B) (xs : vec A n) : vec B n \elim n, xs
  | 0, _ => ()
  | suc n, (x, xs) => (f x, map1 f xs)


\func map2 {A B : \Type} {n : Nat} (f : A -> B) (xs : Vec A n) : Vec B n \elim n, xs
  | 0, fnil => fnil
  | suc _, fcons x xs => fcons (f x) (map2 f xs)

-- 6. Реализуйте аналоги функции zipWith для vec и Vec.
--    Функция должна принимать вектора одинаковой длины.

\func zipWith1 {A B C : \Type} {n : Nat} (f : A -> B -> C) (xs : vec A n) (ys : vec B n) : vec C n \elim n, xs, ys
  | 0, _, _ => ()
  | suc n, (x, xs), (y, ys) => (f x y, zipWith1 f xs ys)


\func zipWith2 {A B C : \Type} {n : Nat} (f : A -> B -> C) (xs : Vec A n) (ys : Vec B n) : Vec C n \elim n, xs, ys
  | 0, _, _ => fnil
  | suc _, fcons x xs, fcons y ys => fcons (f x y) (zipWith2 f xs ys)

-- 7. Реализуйте Fin как рекурсивную функцию.

\data Empty

\data NAT (A : \Type)
    | S A | Z

\func fin (n : Nat) : \Type \elim  n
    | zero  => Empty
    | suc n => NAT (fin n)

\func fin0 : fin 3 => Z
\func fin1 : fin 3 => S Z
\func fin2 : fin 3 => S (S Z)

-- 8. Функции Fin n → A соответствуют спискам элементов A длины n.
--    Функция, преобразующая Vec A n в Fin n → A, была реализована на лекции.
--    Реализуйте обратную функцию.

\func coin {A : \Type} {n : Nat} (f : Fin n -> A) : Vec A n \elim n
    | 0 => fnil
    | suc _ => fcons (f fzero) (coin (\lam fin' => f (fsuc fin')))

-- 9. Определите тип матриц и ряд функций над ними.

\func Mat (A : \Type) (n m : Nat) : \Type => Vec (Vec A m) n

-- диагональная матрица с элементами e на диагонали и z на остальных позициях.

\func \infix 6 == (x y : Nat) : Bool \elim x, y
    | 0, 0 => true
    | suc _ , 0 => false
    | 0, suc _ => false
    | suc x,  suc y => x == y


\func row {A : \Type} (z e : A) (n k : Nat) : Vec A n \elim n
    | 0 => fnil
    | suc n => \let tail => row z e n k \in if ((suc n) == k) ((fcons e) tail) ((fcons) z tail)

\func diag {A : \Type} (z e : A) (n : Nat) : Mat A n n => helper z e n n
    \where \func helper {A : \Type} (z e : A) (n m : Nat) : Mat A m n \elim m
    | 0 => fnil
    | suc x => fcons (row z e n x) (helper z e n x)

-- транспонирование матриц

\func transpose {A : \Type} {n m : Nat} (M : Mat A n m) : Mat A m n \elim n, m
    | 0, m => replicate2 fnil m
    | _, 0 => fnil
    | suc _, suc m => fcons (map2 Head M) (transpose (map2 Tail M))

-- сложение матриц

\func matAdd {A : \Type} (add : A -> A -> A) (n m : Nat) (M N : Mat A n m) : Mat A n m =>
    zipWith2 (zipWith2 add) M N

-- умножение матриц

-- z ─ нейтральный элемент по сложению.
\func matMul {A : \Type} (z : A) (add mul : A -> A -> A) (n m k : Nat) (M : Mat A n m) (N : Mat A m k) : Mat A n k =>
  \let Nt => transpose N \in
  map2 (\lam r =>
      \let row => replicate2 r k \in
      map2 (foldr2 add z) (matAdd mul k m row Nt)) M
  \where
    \func foldr2 {A B : \Type} {n : Nat} (f : A -> B -> B) (acc : B) (vs : Vec A n) : B \elim n, vs
      | 0, _              => acc
      | suc n, fcons v vs => f v (foldr2 f acc vs)

-- 10. Определите тип CTree A n бинарных деревьев высоты ровно n, которые хранят элементы только во внутренних узлах, но не в листьях.
--     Высота листа равна 0.
--     Любое такое бинарное дерево будет полным.

\data CTree (A : \Type) (n : Nat) \elim n
    | 0 => leaf
    | suc n => node A (CTree A n) (CTree A n)

-- 11. Определите тип Tree A n бинарных деревьев высоты не больше n, которые хранят элементы только во внутренних узлах, но не в листьях.
--     Высота листа равна 0.

\data Tree (A : \Type) (n : Nat) \elim n
  | _ => leaf'
  | suc n => node' (Tree A n) A (Tree A n)

-- определите функцию, возвращающую высоту дерева.

\func height {A : \Type} (n : Nat) (t : Tree A n) : Fin (suc n) \elim n, t
  | _, leaf'  => fzero
  | suc n, node' l _ r => fsuc (maxF (height n l) (height n r))
  \where \func maxF {N : Nat} (a b : Fin N) : Fin N => if ((toNat a) < (toNat b)) b a
